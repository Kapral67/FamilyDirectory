plugins {
    id 'java-library'
}

group = "${rootProject.group}.assets"

dependencies {
    api platform(libs.aws.sdk.bom)
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    implementation libs.jackson.databind
    implementation libs.jug
    implementation libs.googlecode.libphonenumber
    implementation libs.commons.codec
    implementation libs.commons.validator
    implementation libs.apache.commons.text
    implementation libs.apache.httpcomponents.httpcore
    implementation libs.aws.cdk.lib
    implementation libs.aws.lambda.java.core
    implementation libs.aws.lambda.java.events
    implementation 'software.amazon.awssdk:dynamodb-enhanced'
    implementation 'software.amazon.awssdk:sesv2'
    implementation 'software.amazon.awssdk:amplify'
}

tasks.register('generateBuildProperties') {
    def version = project.version
    def srcFile = layout.buildDirectory.file(
        "generated/sources/assets/org/familydirectory/assets/BuildProperties.java"
    ).get().asFile
    outputs.file srcFile
    doLast {
        srcFile.text = """package org.familydirectory.assets;
enum BuildProperties {
    ;
    static final double VERSION = ${version};
}
"""
    }
}

compileJava.dependsOn generateBuildProperties
sourceSets.main.java.srcDirs += layout.buildDirectory.dir("generated/sources/assets").get().asFile
