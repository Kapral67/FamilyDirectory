plugins {
    id 'java-library'
}

group = "${rootProject.group}.assets"

dependencies {
    implementation libs.jackson.databind
    implementation libs.jetbrains.annotations
    implementation libs.googlecode.libphonenumber
    implementation libs.commons.validator
    implementation libs.apache.commons.text
    api libs.apache.httpcomponents.httpcore
    api libs.slf4j.nop
    api libs.aws.cdk.lib
    api libs.aws.lambda.java.core
    api libs.aws.lambda.java.events
    api platform(libs.aws.sdk.bom)
    implementation 'software.amazon.awssdk:dynamodb-enhanced'
    implementation 'software.amazon.awssdk:sesv2'
    implementation 'software.amazon.awssdk:amplify'
}

tasks.register('generateBuildProperties') {
    def version = project.version
    def srcFile = layout.buildDirectory.file(
        "generated/sources/assets/org/familydirectory/assets/BuildProperties.java"
    ).get().asFile
    outputs.file srcFile
    doLast {
        srcFile.text = """package org.familydirectory.assets;
enum BuildProperties {
    ;
    static final double VERSION = ${version};
}
"""
    }
}

compileJava.dependsOn generateBuildProperties
sourceSets.main.java.srcDirs += layout.buildDirectory.dir("generated/sources/assets").get().asFile
