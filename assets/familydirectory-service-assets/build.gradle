plugins {
    id 'java-library'
}

group = "${rootProject.group}.assets"

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.13.41'
    implementation 'commons-validator:commons-validator:1.9.0'
    implementation 'org.apache.commons:commons-text:1.12.0'
    api 'org.apache.httpcomponents:httpcore:4.4.16'
    api 'org.slf4j:slf4j-nop:2.0.13'
    api 'software.amazon.awscdk:aws-cdk-lib:2.149.0'
    api 'com.amazonaws:aws-lambda-java-core:1.2.3'
    api 'com.amazonaws:aws-lambda-java-events:3.12.0'
    api platform('software.amazon.awssdk:bom:2.31.1')
    implementation 'software.amazon.awssdk:dynamodb-enhanced'
    implementation 'software.amazon.awssdk:sesv2'
    implementation 'software.amazon.awssdk:amplify'
}

tasks.register('generateBuildProperties') {
    def version = project.version
    def srcFile = layout.buildDirectory.file(
        "generated/sources/assets/java/main/org/familydirectory/assets/BuildProperties.java"
    ).get().asFile
    outputs.file srcFile
    doLast {
        srcFile.text = """package org.familydirectory.assets;
import java.util.Objects;
enum BuildProperties {
    VERSION(Double.valueOf("${version}"));
    private final Object property;
    BuildProperties(final Object property) {
        this.property = Objects.requireNonNull(property);
    }
    @Override
    public final String toString() {
        return property.toString();
    }
    public final Object getProperty() {
        return property;
    }
}
"""
    }
}

compileJava.dependsOn generateBuildProperties
sourceSets.main.java.srcDirs += layout.buildDirectory.dir("generated/sources/assets").get().asFile
